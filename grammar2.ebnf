program = {variableDeclaration | functionDefinition};

statement = ifStatement |
            whileStatement |
            variableDeclaration |
            assignment |
            functionCall |
            returnStatement;

functionDefinition = functionReturnType identifier parameterList statementBlock;

parameterList = "(" [type identifier, {"," type identifier}] ")";

statementBlock = "{" {statement} "}";

ifStatement = "if" "(" expression ")" statementBlock ["else" statementBlock];

whileStatement = "while" "(" expression ")" statementBlock;

variableDeclaration = type identifier ["=" expression];

assignment = identifier "=" expression |
             identifier "[" expression "]" "=" expression;

functionCall = identifier argumentList;

argumentList = "(" [expression, {"," expression}] ")";

returnStatement = "return" expression;

expression = andExpression {orOperator andExpression};

andExpression = relationExpression {andOperator relationExpression};

relationExpression = equalExpression {relationOperator equalExpression};

equalExpression = additiveExpression {equalExpression additiveExpression};

additiveExpression = multiplicativeExpression {additiveOperator multiplicativeExpression};

multiplicativeExpression = possiblyNonNullableExpression {multiplicativeOperator possiblyNonNullableExpression};

possiblyNonNullableExpression = possiblyNegatedSingleExpression ["?"];

possiblyNegatedSingleExpression = ["!" | "-"] singleExpression;

singleExpression = identifier |
                   literal |
                   "(" expression ")" |
                   functionCall |
                   identifier "[" expression "]";

functionReturnType = type | "void";

type = simpleType | parametrizedType;

parametrizedType = collectionType "<" simpleType "," simpleType ">";

collectionType = "dict";

simpleType = "int" |
             "float" |
             "string";

equalOperator = "==" |
                "!=";

relationOperator = "<" |
                   ">" |
                   "<=" |
                   ">=";

orOperator = "||";

andOperator = "&&";

additiveOperator = "+" |
                   "-";

multiplicativeOperator = "*" |
                         "/"
                         "%";

identifier = (letter | "_") {letter | digit | "_"};

literal = stringLiteral |
          numberLiteral |
          dictionaryLiteral |
          nullLiteral;

letter = "a".."z" | "A".."Z";

digit = "0" | nonZeroDigit;

nonZeroDigit = "1" .. "9";

stringLiteral = "\"" .. "\"";

numberLiteral = nonZeroDigit {digit} ["." digit {digit}];

dictionaryLiteral = "{" [expression ":" expression {"," expression ":" expression }] "}";

nullLiteral = "null";